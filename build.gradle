buildscript {
    repositories {
        maven {
            url "http://repo.bodar.com"
        }
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.commercehub:gradle-cucumber-jvm-plugin:0.8"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'com.commercehub.cucumber-jvm' // https://github.com/commercehub-oss/gradle-cucumber-jvm-plugin

group="offernet"
version="0.0.3"

task wrapper(type: Wrapper) {
	gradleVersion = '4.6'
}

task cucumberReporting(type: Exec) {
  executable "sh"
  args "-c", "groovy scripts/build-cucumber-report.groovy"
}

task buildBook(type: Exec) {
    executable "sh"
    args "-c", "scripts/build-offernet-documentation.sh"
}

task copyUnitTestReport(type: Copy) {
    from "${buildDir}/reports/tests/test"
    into "docs/public/unit-tests/"
}

jacocoTestReport {
     reports {
         xml.enabled true
         csv.enabled false
         html.enabled true
     }
}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
    reports.junitXml.enabled = false
    reports.html.enabled = true
    finalizedBy cucumberReporting
    finalizedBy jacocoTestReport
}

task buildDocumentation {
  dependsOn 'buildBook'
  dependsOn 'copyUnitTestReport'
  tasks.findByName('copyUnitTestReport').mustRunAfter 'buildBook'
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
	jcenter()
}

dependencies {
	compile 'junit:junit:4.12'
	compile 'org.codehaus.groovy:groovy-all:2.4.11'
	compile 'com.datastax.dse:dse-java-driver-graph:1.5.1'
	compile 'org.apache.tinkerpop:gremlin-core:3.3.1'
	compile 'log4j:log4j:1.2.17'
	compile 'org.slf4j:slf4j-log4j12:1.7.22'
	compile 'com.typesafe.akka:akka-actor_2.12:2.5.11'
	compile 'com.typesafe.akka:akka-actor-typed_2.12:2.5.11'
  compile 'info.debatty:java-string-similarity:1.1.0'
	testCompile 'com.typesafe.akka:akka-testkit_2.12:2.5.11'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'net.masterthought:cucumber-reporting:3.1.0'
}


tasks.withType(JavaCompile) {
//    options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-proc:none'
//    options.compilerArgs << '-Xlint:unchecked'
    options.warnings = false
}